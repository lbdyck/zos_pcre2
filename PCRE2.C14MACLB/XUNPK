         MACRO
         XUNPK &IN,&INL,&OUT
.**********************************************************************
.*
.* Created by John Gateley
.*
.*       This macro unpacks character data into hexadecimal format.
.*       so X'C1F16D' or C'A1_' becomes C'C1F16D'
.*
.*       In the above example
.*       XUNPK =X'C1F16D',3,OUT_TXT
.*
.*       XUNPK FIELDA,,OUT_TXT          generates L'FIELDA for length
.*
.*
.* indicate that XUNPK macro was used so that the table can be
.* defined in PRGESTAT
.*
.* this is the table that PRGESTAT will define
.* XUNPACK_TAB    DC    C'0123456789ABCDEF'
.*
         AIF    (N'&SYSLIST NE 3).BADPARM
.*
         GBLC  &XUNPCK
&XUNPCK  SETC  'Y'
.*
         AIF   ('&IN'(1,1) EQ '(').REGVAL   * value in register
         LA    R14,&IN                   * address of input string
         AGO   .NOTREG
.REGVAL  ANOP
&INR     SETC  '&IN'(2,K'&IN-2)
         ST    &INR,LEN_SAY              * this exists in PRGDEF
         LA    R14,LEN_SAY               * address of input string
         LA    R15,4                     * it is 4 bytes
         AGO   .LENDON
.*
.NOTREG  ANOP
         AIF   (T'&INL EQ 'O').NOLEN     * parameter missing
         AIF   ('&INL'(1,1) EQ '(').REGVAL2   * value in register
         LA    R15,&INL                  * length of input string
         AGO   .LENDON
.REGVAL2 ANOP
&INR2    SETC  '&INL'(2,K'&INL-2)
         LR    R15,&INR2
         AGO   .LENDON
.NOLEN   ANOP
         LA    R15,L'&IN
.*
.LENDON  ANOP
         LA    R1,&OUT                   * address of output area
B#&SYSNDX      DS    0H
         MVC   HEXPACK_W2(1),0(R14)      * copy one byte
         UNPK  HEXPACK_W3,HEXPACK_W2     * UNPK two bytes to three
         NC    HEXPACK_W3(2),=X'0F0F'    * clear zones
         TR    HEXPACK_W3(2),XUNPACK_TAB * convert to displayable
         MVC   0(2,R1),HEXPACK_W3        * copy to receiver
         LA    R14,1(,R14)               * next in input
         LA    R1,2(,R1)                 * next in output
         BCT   R15,B#&SYSNDX             * subtract and branch
         MEXIT
.BADPARM MNOTE 16,'invalid positional parameters, need 3'
         MEXIT
         MEND
