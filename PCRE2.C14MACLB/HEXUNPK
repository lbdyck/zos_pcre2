         MACRO
         HEXUNPK &IN,&INL,&OUT
.**********************************************************************
.*
.* September 2015 - John Gateley - Create
.*
.*       This macro unpacks character data into hexadecimal format.
.*       so X'C1F16D' or C'A1_' becomes C'C1F16D'
.*
.*       In the above example
.*       HEXUNPK =X'C1F16D',3,OUT_TXT
.*
.* indicate that HEXUNPK macro was used so that the table can be
.* defined in PRGESTAT
.*
         GBLC  &XUNPCK
&XUNPCK  SETC  'Y'
.*
         LA    R14,&IN                   * address of input string
         LA    R15,&INL                  * length of input string
         LA    R1,&OUT                   * address of output area
B#&SYSNDX      DS    0H
         MVC   HEXPACK_W2(1),0(R14)      * copy one byte
         UNPK  HEXPACK_W3,HEXPACK_W2     * UNPK two bytes to three
         TR    HEXPACK_W3(2),XUNPACK_TAB-240 * convert to displayable
         MVC   0(2,R1),HEXPACK_W3        * copy to receiver
         LA    R14,1(,R14)               * next in input
         LA    R1,2(,R1)                 * next in output
         BCT   R15,B#&SYSNDX             * subtract and branch
         MEND
