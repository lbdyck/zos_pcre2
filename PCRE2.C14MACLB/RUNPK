         MACRO
         RUNPK &REG,&OUT
.**********************************************************************
.*
.* Created by John Gateley
.*
.*       This macro unpacks a register into hexadecimal format.
.*
.*       Warning it updates byte 9 of the output field
.*       but then blanks it out.
.*
.*       RUNPK R13,OUT_TXT
.*       RUNPK (R13),OUT_TXT
.*
.* this is the table that PRGSTAT will define, if not using PRGDEF
.* and associated macros then define it yourself
.*
.* XUNPACK_TAB    DC    C'0123456789ABCDEF'
.*
         AIF    (N'&SYSLIST NE 2).BADPARM
.*
.* indicate that RUNPK macro was used so that the table can be
.* defined in PRGSTAT
         GBLC  &XUNPCK
&XUNPCK  SETC  'Y'
.*
         AIF   ('&REG'(1,1) EQ '(').REGVAL  * reg in parentheses
         AGO   .NOTREG
.REGVAL  ANOP
&INR     SETC  '&REG'(2,K'&REG-2)
         AGO   .AFTREG
.*
.NOTREG  ANOP
&INR     SETC  '&REG'
.*
.AFTREG  ANOP
         ST    &INR,&OUT               store into first 4 bytes
         UNPK  &OUT.(9),&OUT.(5)       unpk 5 into 9
         MVI   &OUT+8,C' '             blank the ninth byte
         NC    &OUT.(8),=X'0F0F0F0F0F0F0F0F'   clear the zones
         TR    &OUT.(8),XUNPACK_TAB    convert to displayable
         MEXIT
.BADPARM MNOTE 16,'invalid positional parameters, need 2'
         MEXIT
         MEND
