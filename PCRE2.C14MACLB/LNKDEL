         MACRO
         LNKDEL  &START=,&NEXT=,&LENGTH=,&REG=
.**********************************************************************
.*
.*       This macro frees a linked list of getmained storage.
.*       Macro LNKBLD builds the storage.
.*
.*       &START=     holds the address of the first area
.*       &NEXT=      pointer to the next area from this one
.*       &LENGTH=    length of the getmained area
.*       &REG=       general purpose register to use
.*
         AIF   (T'&START  EQ 'O').NOSTART  * parameter missing
         AIF   (T'&NEXT   EQ 'O').NONEXT   * parameter missing
         AIF   (T'&LENGTH EQ 'O').NOLEN    * parameter missing
         AIF   (T'&REG    EQ 'O').NOREG    * parameter missing
.*
         L     &REG,&START                 * point to first entry
A&SYSNDX       DS    0H
         LAY   R2,&LENGTH                  * length of storage area
         LR    R1,&REG                     * copy address
         L     &REG,&NEXT                  * point to next slot
         STORAGE RELEASE,LENGTH=(R2),ADDR=(R1)
         LTR   &REG,&REG                   * check if finished
         JNZ   A&SYSNDX                    * no so go do next
.*
         MEXIT
.NOSTART MNOTE 16,'Start anchor not provided'
         MEXIT
.NONEXT  MNOTE 16,'Next field not provided'
         MEXIT
.NOLEN   MNOTE 16,'Length not provided'
         MEXIT
.NOREG   MNOTE 16,'Register not provided'
         MEND
